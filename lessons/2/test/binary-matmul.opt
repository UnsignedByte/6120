@main(a: int, b: int, dim1: int, dim2: int, dim3: int) {
  one: int = const 1;
  i: int = const -1;
  output: int = const 0;
.row_loop:
  j: int = const -1;
  i: int = add i one;
  i_lt_dim1: bool = lt i dim1;
  br i_lt_dim1 .col_loop .return;
.col_loop:
  k: int = const -1;
  j: int = add j one;
  dot_product: int = const 0;
  j_lt_dim3: bool = lt j dim3;
  br j_lt_dim3 .inner_loop .row_loop;
.inner_loop:
  k: int = add k one;
  k_lt_dim2: bool = lt k dim2;
  br k_lt_dim2 .multiply .end_col;
.multiply:
  a_bit: int = call @mat_bitsel a dim2 i k;
  b_bit: int = call @mat_bitsel b dim3 k j;
  a_bit_b_bit: int = mul a_bit b_bit;
  dot_product: int = add a_bit_b_bit dot_product;
  jmp .inner_loop;
.end_col:
  dot_product_odd: bool = call @is_odd dot_product;
  br dot_product_odd .add_dp .col_loop;
.add_dp:
  index: int = call @mat_packed_index i j dim3;
  dp_bit: int = call @pow2 index;
  output: int = add dp_bit output;
  jmp .col_loop;
.return:
  print output;
}
@mat_bitsel(m: int, cols: int, i: int, j: int): int {
  index: int = call @mat_packed_index i j cols;
  ret_val: bool = call @bitsel m index;
  br ret_val .ret_one .ret_zero;
.ret_one:
  one: int = const 1;
  ret one;
.ret_zero:
  zero: int = const 0;
  ret zero;
}
@mat_packed_index(i: int, j: int, cols: int): int {
  v0: int = mul i cols;
  index: int = add v0 j;
  ret index;
}
@pow2(n: int): int {
  one: int = const 1;
  two: int = const 2;
  i: int = const 0;
  result: int = const 1;
.loop:
  i_lt_n: bool = lt i n;
  br i_lt_n .multiply .return;
.multiply:
  result: int = mul result two;
  i: int = add i one;
  jmp .loop;
.return:
  ret result;
}
@bitsel(m: int, i: int): bool {
  zero: int = const 0;
  one: int = const 1;
  two: int = const 2;
.loop:
  i_eq_zero: bool = eq i zero;
  br i_eq_zero .return .divide;
.divide:
  m: int = div m two;
  i: int = sub i one;
  jmp .loop;
.return:
  m_bit: bool = call @is_odd m;
  ret m_bit;
}
@abs(n: int): int {
  zero: int = const 0;
  is_neg: bool = lt n zero;
  br is_neg .negative .positive;
.negative:
  n: int = sub zero n;
.positive:
  ret n;
}
@is_even(n: int): bool {
  n0: int = call @abs n;
  one: int = const 1;
  two: int = const 2;
  np1: int = add n0 one;
  half: int = div n0 two;
  np1_half: int = div np1 two;
  ret_val: bool = eq half np1_half;
  ret ret_val;
}
@is_odd(n: int): bool {
  is_even: bool = call @is_even n;
  ret_val: bool = not is_even;
  ret ret_val;
}
